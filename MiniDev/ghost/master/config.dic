Select.configmenu
{
	TalkConfig
}

Select.format
{
	formattype = 1 //back to config
	--
	"\_q\b[2]Current Formating: %(distingstart)%(distingtype) text\f[default]\n\n[half]"
	--
	"\![*]\q[Bold text,OnDisting,bold]\n\![*]\q[Cursive text,OnDisting,cursive]\n/
	\![*]\q[Striked text,OnDisting,strike]\n\![*]\q[Underlined text,OnDisting,underline]\n/
	\![*]\q[Yellow text,OnDisting,yellow]\n\![*]\q[Green text,OnDisting,green]\n/
	\![*]\q[Red text,OnDisting,red]\n\![*]\q[Pink text,OnDisting,pink]\n\![*]\q[Custom Color,OnDisting,custom]\n\n\n[half]"
	--
	"\![*]\q[Go back,TalkConfig] | \![*]\q[Nevermind,cancle]"
}

OnFormattingNar
{
	if reference0 == "config"; formattype = 1 //back to config
	--
	"\_q\1\b[2]Current Formating: %(distingnarr)%(nardistingtype) text\f[default]\n\n[half]"
	--
	"\![*]\q[Bold text,OnDisting,bold,nar]\n\![*]\q[Cursive text,OnDisting,cursive,nar]\n/
	\![*]\q[Striked text,OnDisting,strike,nar]\n\![*]\q[Underlined text,OnDisting,underline,nar]\n/
	\![*]\q[Yellow text,OnDisting,yellow,nar]\n\![*]\q[Green text,OnDisting,green,nar]\n/
	\![*]\q[Red text,OnDisting,red,nar]\n\![*]\q[Pink text,OnDisting,pink]\n\![*]\q[Custom Color,OnDisting,custom,nar]\n\n\n[half]"
	--
	"\![*]\q[Go back,TalkConfig] | \![*]\q[Nevermind,cancle]"
}

Select.hemisph
{
	if hsphere == "Northern"; hsphere = "Southern"
	else; hsphere = "Northern"
	--
	TalkConfig
}

TalkConfig
{
	"\0\b4\_q\![set,autoscroll,disable]\*" 
	--
	"\f[bold,1]\__q[OnZChnage,uiclps,%(uiclps),set]----User Info----\__q\f[default]\n\n[half]"
	--
	if uiclps == 1
	{
		"\f[bold,1]Your Name:\f[default] %(acc)\q[%(username),TEACHNAME]\f[default]\n"
		--
		"\f[bold,1]Your Nickname:\f[default] %(acc)\q[%(nickname),nicknamechange]\f[default]\n"
		--
		"\f[bold,1]Pronouns:\f[default] %(acc)\q[%(heshe)/%(himher)/%(hisher),TEACHPRONOUN]\f[default]\n"
		--
		"\f[bold,1]Birthday:\f[default] %(acc)\q[%(birthdayprint),TEACHBDAY]\f[default]\n"
		--
		"\f[bold,1]Hemisphere:\f[default] %(acc) \q[%(hsphere),hemisph]\f[default]\n\n[half]"
	}
	--
	"\f[bold,1]\__q[OnZChnage,mdclps,%(mdclps),set]----MD Preferences----\__q\f[default]\n\n[half]"
	--
	if mdclps == 1
	{
		"\f[bold,1]Dev Name:\f[default] %(acc)\q[%(devname),script:\![open,inputbox,OnDevName,-1\]]\f[default]\n"
		--
		"\f[bold,1]Hour Notice:\f[default] %(acc)\q[%(hrnt),OnButtonChange,hrnt,%(hrnt)]\f[default]\n"
		--
		"\f[bold,1]Notice other windows:\f[default] %(acc)\q[%(noticewin),OnButtonChange,noticewin,%(noticewin)]\f[default]\n"
		--
		if repairglitch == 1 && tentruth == 1; "\f[bold,1]Disable %(devname) Talking:\f[bold,0] %(acc)\q[%(MDisable),OnButtonChange,MDisable,%(MDisable)]\f[default]\n"
		--
		"\f[bold,1]Talk Rate:\f[default] %(acc)\__q[talkinter]%(talktime)\__q\f[default]\n"
		--
		"\n[half]\f[bold,1]\q[Change Traits,traits]\f[default]\n\n[half]"	
		--
		"%(acc)\q[Change Accent Color,OnAccColor]\f[default]\n"
		--
		if repairglitch == 1 && tentruth == 1; "%(distingstart)\q[Change M-201z Formatting,format]\f[default]\n"
		--
		if narrator != 0; "%(distingnarr)\q[Change Narrator Formatting,OnFormattingNar,config]\f[default]\n\n[half]"
	}
	--
	"\__q[OnZChnage,qbtnclps,%(qbtnclps),set]\f[bold,1]----Quick tools----\__q\n\n[half]\f[default]"
	--
	if qbtnclps == 1; "Quick tools: %(acc) \q[%(tooln),OnButtonChange,tooln,%(tooln)]\f[default]\n\n[half]"
	--
	if tooln == "ON" && qbtnclps == 1
	{
		"light/dark button: %(acc) \q[%(dlbtn),OnButtonChange,dlbtn,%(dlbtn)]\f[default]\n"
		--
		"Logs: %(acc) \q[%(logbtn),OnButtonChange,logbtn,%(logbtn)]\f[default]\n"
		--
		"Weather: %(acc) \q[%(webtn),OnButtonChange,webtn,%(webtn)]\f[default]\n"
		--
		"Settings: %(acc) \q[%(setbtn),OnButtonChange,setbtn,%(setbtn)]\f[default]\n"
		--
		"Statistics: %(acc) \q[%(statbtn),OnButtonChange,statbtn,%(statbtn)]\f[default]\n"
		--
		"To-Do: %(acc) \q[%(todobtn),OnButtonChange,todobtn,%(todobtn)]\f[default]\n"
		--
		"Cuztomization: %(acc) \q[%(cusbtn),OnButtonChange,cusbtn,%(cusbtn)]\f[default]\n"
		--
		"Open App: %(acc) \q[%(appbtn),OnButtonChange,appbtn,%(appbtn)]\f[default]\n"
		--
		"\n[half]Sticky tools: %(acc) \q[%(sticky),sticbtn,%(sticbtn)]\f[default]\n"	
		--
		"\![*]\q[Reset Tools Positions,OnMoveQTool]\n\n[half]"
	}
	--
	"\__q[OnZChnage,menclps,%(menclps),set]\f[bold,1]----Menu----\__q\n\n[half]\f[default]"
	--
	if menclps == 1
	{
		"\f[bold,1]%(acc)\q[%(timeformatype),timetype]\f[default] TIME\n"
		--
		"\f[bold,1]DATE FORMAT:%(acc)\q[%(dateformatype),datetype]\f[default]\n\n[half]"
		--
		if weathermenu != "ON"
		{
			"Weather: %(acc) \q[%(weathermenu),OnButtonChange,weathermenu,%(weathermenu)]\f[default]\n"
		}
		else
		{
			"Weather %(acc) \q[%(weathermenu),OnButtonChange,weathermenu,%(weathermenu)]\f[default] ("
			--
			"%(acc)\q[%(currentweatherarray[1]),startunit]\f[default])\n"
		}
		--
		"Moon Phase: %(acc) \q[%(moonmenu),OnButtonChange,moonmenu,%(moonmenu)]\f[default]\n\n[half]"
	}
	--
	"\__q[OnZChnage,othclps,%(othclps),set]\f[bold,1]----Other----\__q\n\n[half]\f[default]"
	--
	if othclps == 1
	{
		"Auto update check: %(acc) \q[%(upchec),OnButtonChange,upchec,%(upchec)]\f[default]\n"
		--
		"\f[default]Enable Uninstall?: %(acc) \q[%(deleteshitai),OnButtonChange,deleteshitai,%(deleteshitai)]\f[default]\n\n[half]"
	}
	--
	"\n[half]\![*]\q[Back to the menu,BACKTOMENU]\n"
    --
	"\![*]\q[Nevermind,CANCEL]\e"
}

Select.sticbtn
{
	case sticky
	{
		when "ON"
		{
			"\![reset,sticky-window]"
			--
			sticky = "OFF"
		}
		when "OFF"
		{
			"\![set,sticky-window,1,0,3,4,5,6,7,8,9,10]"
			--
			sticky = "ON"
		}
	}
	--
	TalkConfig
}

OnDevName
{
	devname = reference0
	--
	TalkConfig
}

Select.TEACHPRONOUN
{
	"\0What pronouns do you want?\w5\n\n[half]\_q/
	\![*]\q[He-His-Him,choicemasc]\n/
	\![*]\q[She-Her-Her,choicefem]\n/	
	\![*]\q[They-Their-Them,choiceneither]\n/
    \![*]\q[Your own,choiceown]\n\n[half]/
	\![*]\q[Back to main menu,BACKTOMENU]\n/
	\![*]\q[Nevermind,CANCEL]\e"
}

Select.choicemasc
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	--
	truepresuffix = presuffix
	truehimher = himher
	trueheshe = heshe
	truehisher = hisher
	--
	"\0The pronouns are now he him and his.\e"
}

Select.choicefem
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	--
	truepresuffix = presuffix
	truehimher = himher
	trueheshe = heshe
	truehisher = hisher
	--
	"\0The pronouns are now she her and her.\e"
}

Select.choiceneither
{
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	--
	truepresuffix = presuffix
	truehimher = himher
	trueheshe = heshe
	truehisher = hisher
	--
	"\0The pronouns are now they them and their.\e"
}

Select.choiceown
{
	"\0Okay then, write them as replacment of the ones I say\x\creplacement of 'he'\![open,inputbox,OnOwnPronone,-1]"
}

OnOwnPronone
{ 
	heshe = reference0
	"\0%(heshe)\w8\w8 replacement of 'him'\![open,inputbox,OnOwnProntwo,-1]"
}

OnOwnProntwo
{
	himher = reference0
	"\0%(himher)\w8\w8 replacement of 'his'\![open,inputbox,OnOwnPronthree,-1]"
}

OnOwnPronthree
{
	hisher = reference0
	"\0%(hisher)\w8\w8 Your pronounces: %(heshe)/%(himher)/%(hisher)\e"
}

Select.TEACHBDAY
{
	dayslot = GetDaySlot 
	"\0What month were you born? \w8Write the number.\![open,inputbox,OnUserBornMonth,-1]\e"
}


OnUserBornMonth
{
	_tempmonth = reference0 
	if _tempmonth >= 1 && _tempmonth <= 12
	{
		bornmonth = _tempmonth
		bornmonthprint = Printmonth
		"\0So %(bornmonthprint)?\n\w8What day was it?\![open,inputbox,OnUserBornDay,-1]\e"
	}
	else 
	{
	"\0There is no month like that,\w5 try again\![open,inputbox,OnUserBornMonth,-1]\e"
	}
}

Printmonth
{
	if bornmonth == 1; "January"
	elseif bornmonth == 2; "February"
	elseif bornmonth == 3; "March"
	elseif bornmonth == 4; "April"
	elseif bornmonth == 5; "May"
	elseif bornmonth == 6; "June"
	elseif bornmonth == 7; "July"
	elseif bornmonth == 8; "August"
	elseif bornmonth == 9; "September"
	elseif bornmonth == 10; "October"
	elseif bornmonth == 11; "November"
	else; "December"
}

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31; "%(bornday)st"
	elseif bornday == 2 || bornday == 22; "%(bornday)nd"
	elseif bornday == 3 || bornday == 23; "%(bornday)rd"
	else; "%(bornday)th"
}	

OnUserBornDay
{
	tempday = reference0
	
	if bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12 //This is checking to see if it's one of the months with 31 days.
	{
		if tempday >= 1 && tempday <= 31 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
			"%(birthchecked)"
		}
		else 
		{
			"\0There is no day like that\w5, try again\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}
	elseif bornmonth == 2
	{
		if tempday >= 1 && tempday <= 29
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
			"%(birthchecked)"
		}
		else
		{
			"\0There is no day like that\w5, try again\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}
	else 
	{
		if tempday >= 1 && tempday <= 30 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
			"%(birthchecked)"
		}
		else
		{
			"\0There is no day like that\w5, try again\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}		
}

birthchecked
{
	askzoidac = 0
	zodiaccheck = 0
	truebirth = userbirthday
	--
	if userbirthday == dayslot; "\0Your birthday\w6 is today?\w7Happy Birthday then%(users)!"
	elseif userbirthday == "10month 31day"; "\0Your birthday is on halloween?\w6 does that mean twice as many candy and costume party?"
	else; "\0Your birthday is %(bornmonthprint) %(borndayprint)? I will remember"
}

Select.talkinter
{
	"\0\b2How often should I speak?\w8\0currently it's set to '%(talktime)'./
	//note the new envelope here for %(talktime). You'll see it again in a second. You can replace the dialogue above with whatever you like, just make sure it ends with ./ and not \e.
	\_q\0\n\n[half]/
	\![*]\q[Every 30 seconds,TALKMAX]\n/
	\![*]\q[Every minute,TALKHIGH]\n/
	\![*]\q[Every 3 minutes,TALKNORMAL]\n/
	\![*]\q[Every 5 minutes,TALKPOOR]\n/
	\![*]\q[Every 15 minutes,TALKRARE]\n/
	\![*]\q[Every 30 minutes,TALKOVRARE]\n/
    \![*]\q[Set own,TALKOWN]\n/
	\![*]\q[Don't speak,TALKNONE]\n/
	\![*]\q[Don't change,TalksTill]\n\n[half]/
	\![*]\q[Back to main menu,BACKTOMENU]\n/
	\![*]\q[Nevermind,CANCEL]"
}

Select.TALKOWN
{
	"%(ch)Which time interval do you want to use?\n\n[half]"
	--
	"\![*]\q[Seconds,OnTalkCustom,seconds]\n/
	\![*]\q[Minutes,OnTalkCustom,minutes]\n/
	\![*]\q[Hours,OnTalkCustom,hours]"
}

OnTalkCustom
{
	talkinttima = reference0
	--
	case talkinttima
	{
		when "seconds"; "%(ch)I should speak every . . . seconds.\![open,sliderinput,OnTalkCustomize,-1,30,30,59]"
		when "minutes"; "%(ch)I should speak every . . . minutes.\![open,sliderinput,OnTalkCustomize,-1,1,1,59]"
		when "hours"; "%(ch)I should speak every . . . hours.\![open,sliderinput,OnTalkCustomize,-1,1,1,24]"
	}
}

OnTalkCustomize
{
	timecustom = reference0
	--
	case talkinttima
	{
		when "seconds"
		{
			talktime = "%(timecustom) seconds" 
			aitalkinterval = timecustom 
			"I will speak every %(timecustom).\e"
		}
		when "minutes"
		{
			if timecustom == 1
			{
				talktime = "minute" 
				aitalkinterval = timecustom * 60
				"I will speak every minute.\e"
			}
			else
			{
				talktime = "%(timecustom) minutes" 
				aitalkinterval = timecustom * 60
				"I will speak every %(talktime).\e"
			}
		}
		when "hours"
		{
			if timecustom == 1
			{
				talktime = "hour" 
				aitalkinterval = timecustom * 3600
				"I will speak every hour.\e"
			}
			else
			{
				talktime = "%(talktime) hours" 
				aitalkinterval = timecustom * 3600
				"I will speak every %(talktime).\e"
			}
		}
	}
}

Select.TalksTill
{
	"\0No change to the talk rate.\e"
}

Select.TALKMAX
{
	talktime = "30 seconds" //this sets up the envelope properly
	aitalkinterval = 45 //this changes the actual rate
	
	"\0We will speak every 30 seconds.\e"
}

Select.TALKHIGH
{
	talktime = "minute"
	aitalkinterval = 90
	
	"\0I will speak every minute.\e"
}

Select.TALKNORMAL
{
	talktime = "3 minutes"
	aitalkinterval = 270
	
	"\0I will speak every three minutes.\e"
}

Select.TALKPOOR
{
	talktime = "5 minutes"
	aitalkinterval = 400
	
	"\0I will speak every five minutes.\e"
}

Select.TALKNONE
{
	talktime = "...actually I won't talk at all"
	aitalkinterval = 0
	
	"\0I will stay quiet\e"
}

Select.TALKRARE
{
	talktime = "15 minutes"
	aitalkinterval = 1200
	
	"\0I will speak every fifteen minutes.\e"
}

Select.TALKOVRARE
{
	talktime = "30 minutes"
	aitalkinterval = 2400
	
	"\0I will speak every thirty minutes.\e"
}

	
Select.TEACHNAME
{
	OnTeachName	
}

//trait system
Select.traits
{
	"\_q\b2"
	--
    "\f[default]Nice: %(acc) \q[%(nice),OnButtonChange,nice,%(nice),trait]\f[default]\n"		
	--
	if stupid != "ON"; "\f[default]Intelligent: %(acc) \q[%(smart),OnButtonChange,smart,%(smart),trait]\f[default]\n"
	elseif stupid == "ON"; "\f[default]\f[strike,1]Intelligent\f[strike,0]\f[default]\n"		
	--
	if smart != "ON"; "\f[default]Unintelligent: %(acc) \q[%(stupid),OnButtonChange,stupid,%(stupid),trait]\f[default]\n"
	elseif smart == "ON"; "\f[default]\f[strike,1]Unintelligent\f[strike,0]\f[default]\n"		
	--
	"\f[default]Romantic: %(acc) \q[%(romantic),OnButtonChange,romantic,%(romantic),trait]\f[default]\n"	
	--
	"\n[half]\![*]\q[trait info,script:\![open,file,ghost/master/Traits.txt\]]\n"
    --
	"\n[half]\![*]\q[Back to the config menu,configmenu]\n"
    --
	"\![*]\q[Nevermind,CANCEL]\e"
}

Select.triinfo
{
	"\![open,file,ghost/master/Traits.txt]"
}

OnRecomConfig
{
	"\b4\_qInclude | Exclude tags (anime/manga)\n\n[half]"
	--
	"\f[default]BL: %(acc)\q[%(BL),OnButtonChange,BL,%(BL),rec]\f[default]\n"
	--
	"\f[default]GL: %(acc)\q[%(GL),OnButtonChange,GL,%(GL),rec]\f[default]\n"
	--
	"\f[default]Ecchi: %(acc)\q[%(ecchi),OnButtonChange,ecchi,%(ecchi),rec]\f[default]\n"		
	--
	"\f[default]Romance: %(acc)\q[%(romancet),OnButtonChange,romancet,%(romancet),rec]\f[default]\n"
	--
	"\f[default]Action: %(acc)\q[%(action),OnButtonChange,action,%(action),rec]\f[default]\n"
	--
	"\f[default]Drama: %(acc)\q[%(drama),OnButtonChange,drama,%(drama),rec]\f[default]\n"
	--
	"\f[default]Horror: %(acc)\q[%(horror),OnButtonChange,horror,%(horror),rec]\f[default]\n"
	--
	"\n[half]\![*]\q[Back to the config menu,configmenu]\n"
    --
	"\![*]\q[Nevermind,CANCEL]\e"
}

OnRecC
{
	excludedtags = "tagsssss"
	/*
	horror
	drama
	GL
	BL
	ecchi
	romancet
	action
	*/
	--
	if GL == "OFF"; excludedtags += ",GL,Yuri"
	--
	if BL == "OFF"; excludedtags += ",BL,Yaoi"
	--
	if horror == "OFF"; excludedtags += ",Horror"
	--
	if drama == "OFF"; excludedtags += ",Drama"
	--
	if ecchi == "OFF"; excludedtags += ",Ecchi"
	--
	if romancet == "OFF"; excludedtags += ",Romance"
	--
	if action == "OFF"; excludedtags += ",Action"
	--
	OnRecomConfig
}

OnAccColor
{
	"%(cha)What color do you want?\![open,dialog,color,--id=OnCustomAccColor]"
}

OnCustomAccColor
{
	acc = "\f[color,"+ reference2 +"]"
	--
	TalkConfig
}

Select.acces
{

}

Select.degreeez
{
	if "C" _in_ currentweatherarray[1]; currentweatherarray[1] = REPLACE(currentweatherarray[1],"C","F")
	else; currentweatherarray[1] = REPLACE(currentweatherarray[1],"F","C")
	--
	TalkConfig
}

Select.startunit
{
	unitchange = 1
	--
	Select.switchunits
}

Select.datetype
{
	case dateformatype
	{
		when "dd/mm/yyyy"; dateformatype = "yyyy/mm/dd"
		when "yyyy/mm/dd"; dateformatype = "mm/dd/yyyy"
		when "mm/dd/yyyy"; dateformatype = "dd/mm/yyyy"
	}
	--
	TalkConfig
}

Select.timetype
{
	case timeformatype
	{
		when "STANDARD"; timeformatype = "MILITARY"
		when "MILITARY"; timeformatype = "STANDARD"
	}
	--
	TalkConfig
}