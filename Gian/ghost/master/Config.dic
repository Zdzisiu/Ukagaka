Select.configmenu
{
	TalkConfig
}

Select.TEACHPRONOUN
{
	"\0How should I refer to you?\w5\n\n[half]\_q/
	\![*]\q[He-His-Him,choicemasc]\n/
	\![*]\q[She-Her-Her,choicefem]\n/	
	\![*]\q[They-Their-Them,choiceneither]\e"
}

Select.choicemasc
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	--
	"\0I will refere to you as 'he' then\e"
}

Select.choicefem
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	--
	"\0I will refere to you as 'she' then\e"
}

Select.choiceneither
{
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	--
	"\0I will refere to you as 'they' then\e"
}

Select.deleteon
{
	if deleteshitai == "OFF"
	{
		deleteshitai = "ON"
	}
	else
	{
		deleteshitai = "OFF"
	}
	--
	TalkConfig
}

Select.TEACHBDAY
{
	dayslot = GetDaySlot 
	
	"\0What month were you born? \w8Write the number.\![open,inputbox,OnUserBornMonth,-1]\e"
}

OnUserBornMonth
{
	_tempmonth = reference0 
	if _tempmonth >= 1 && _tempmonth <= 12 
	{
		bornmonth = _tempmonth
		bornmonthprint = Printmonth
		--
		"\0So you were born in %(bornmonthprint).\n\w8What day was it?\![open,inputbox,OnUserBornDay,-1]\e"
	
	}
	else 
	{
		"\0There is no month like that. Maybe try again\![open,inputbox,OnUserBornMonth,-1]\e"
	
	}
}

Printmonth
{
	if bornmonth == 1; "January"
	elseif bornmonth == 2; "February"
	elseif bornmonth == 3; "March"
	elseif bornmonth == 4; "April"
	elseif bornmonth == 5; "May"
	elseif bornmonth == 6; "June"
	elseif bornmonth == 7; "July"
	elseif bornmonth == 8; "August"
	elseif bornmonth == 9; "September"
	elseif bornmonth == 10; "October"
	elseif bornmonth == 11; "November"
	else; "December"
}

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31; "%(bornday)st"
	elseif bornday == 2 || bornday == 22; "%(bornday)nd"
	elseif bornday == 3 || bornday == 23; "%(bornday)rd"
	else; "%(bornday)th"
}	


OnUserBornDay
{
	tempday = reference0 
	
	if bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12 
	{
		if tempday >= 1 && tempday <= 31 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
			
			"%(birthchecked)"
		}
		else 
		{
			"\0There is no day like that. Let's try again.\![open,inputbox,OnUserBornDay,-1]\e"
			
		}
	}
	elseif bornmonth == 2 
	{
		if tempday >= 1 && tempday <= 29 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
			
			"%(birthchecked)"

		}
		else
		{
			"\0There is no day like that. Maybe try again.\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}
	else 
	{
		if tempday >= 1 && tempday <= 30 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
			
			"%(birthchecked)"
		}
		else
		{
			"\0There is no day like that. Maybe try again.\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}		
}

birthchecked
{
	if userbirthday == dayslot ; "\0Today is your birthday then.\w8 Happy birthday, %(username).\e"
	else; "\So your birthday is %(bornmonthprint) %(borndayprint)? I will remember.\e"
}


Select.talkinter
{
	"\1\b2How often should Gian speak?\w8\0%(shirtsweekday)The interval is currently set to %(talktime)./
	\n\n[half]/
	\![*]\q[Every 30 seconds,TALKMAX]\n/
	\![*]\q[Every minute,TALKHIGH]\n/
	\![*]\q[Every 3 minutes,TALKNORMAL]\n/
	\![*]\q[Every 5 minutes,TALKPOOR]\n/
	\![*]\q[Every 15 minutes,TALKRARE]\n/
	\![*]\q[Every 30 minutes,TALKOVRARE]\n/
    \![*]\q[Set own,TALKOWN]\n/
	\![*]\q[Don't speak,TALKNONE]\n\n[half]/
	\![*]\q[Don't change,TalksTill]\e"
}

Select.TALKOWN
{
	"How often should I speak?(Write in minutes)\![open,inputbox,OnSpeakTime,-1]"
}

OnSpeakTime
{
	temptalktime = reference0
    if reference0 == 1
    {
        talktime = "minute"
    }
    else
    {
        talktime = reference0 + " minutes"
    }
	--
	aitalkinterval = temptalktime * 90
	
	"I will speak every %(talktime)\e"
}

Select.TalksTill
{
	"\1No change to the talk rate.\e"
}

Select.TALKMAX
{
	talktime = "30 seconds" 
	aitalkinterval = 45 
	
	"\1Gian will speak every 30 seconds.\e"
}

Select.TALKHIGH
{
	talktime = "minute"
	aitalkinterval = 90
	
	"\1Gian will speak every minute.\e"
}

Select.TALKNORMAL
{
	talktime = "3 minutes"
	aitalkinterval = 270
	
	"\1Gian will speak every three minutes.\e"
}

Select.TALKPOOR
{
	talktime = "5 minutes"
	aitalkinterval = 400
	
	"\1Gian will speak every five minutes.\e"
}

Select.TALKRARE
{
	talktime = "15 minutes"
	aitalkinterval = 1200
	
	"\1Gian will speak every fifteen minutes.\e"
}

Select.TALKOVRARE
{
	talktime = "30 minutes"
	aitalkinterval = 2400
	
	"\1Gian will speak every thirty minutes.\e"
}

Select.TALKNONE
{
	talktime = "silence"
	aitalkinterval = 0
	
	"\1Gian is not going to talk at all.\e"
}

Select.TEACHNAME
{
	OnTeachName	
}

Select.CANCEL
{
	counter[0]++
	--
	"\1You decide to not do anything\e"
}

TalkConfig
{
	SetOnOffConf
	--
	"\1\b[2]\_q"
	--
	"\f[bold,1]Your name:\f[default] %(username) \f[sup,1](\q[Change it?,TEACHNAME])\f[sup,0]\n"
	--
	"\f[bold,1]Your pronouns:\f[default] %(heshe)/%(himher)/%(hisher) \f[sup,1](\q[Change them?,TEACHPRONOUN])\f[sup,0]\n"
	--
	"\f[bold,1]Your birthday:\f[default] %(birthdayprint) \f[sup,1](\q[Change it?,TEACHBDAY])\f[sup,0]\n\n\n[half]"
	--
	"\f[bold,1]Talk Rate:\f[default] - every %(talktime) \f[sup,1](\q[Change it,talkinter])\f[sup,0]\n"
	--	
	"\f[default]Enable Uninstall?: \f[color,#71ebc8]\q[%(deleteshitai),OnButtonChange,deleteshitai,%(deleteshitai)]\f[default]\n\n[half]"
	--
	if weathermenu != "ON"
	{
		"Weather: \f[color,#71ebc8]\q[%(weathermenu),OnButtonChange,weathermenu,%(weathermenu)]\f[default]\n"
	}
	else
	{
		"Weather: \f[color,#71ebc8]\q[%(weathermenu),OnButtonChange,weathermenu,%(weathermenu)]\f[default] ("
		--
		"\f[color,#71ebc8]\q[%(currentweatherarray[1]),startunit]\f[default])\n"
	}
	--
	"Moon Phase: \f[color,#71ebc8]\q[%(moonmenu),OnButtonChange,moonmenu,%(moonmenu)]\f[default]\n\n[half]"	
	--
	if locationcode == ""; "Weather Location: \f[color,#71ebc8]\f[bold,1]\![*]\q[Set Location,OnWeatherLocation]\f[default]\n"
	else; "Weather Location: \f[color,#71ebc8]\f[bold,1]\![*]\q[%(curlocation),OnWeatherLocation]\f[default]\n"
	--
	"\f[bold,1]Time Format: \f[color,#71ebc8]\q[%(timeformatype),timetype]\f[default]\n"
	--
	"\f[bold,1]Date Format: \f[color,#71ebc8]\q[%(dateformatype),datetype]\f[default]\n\n[half]"
	--
	"\f[default]Moon Phase: \f[color,#71ebc8] \q[%(moonmenu),moonon]\f[default]\n"
	--
	if moonmenu == "ON"; "\f[default]Hemisphere: \f[color,#71ebc8] \q[%(hsphere),hemisph]\f[default]\n"
    --
	"Auto update check: \f[color,#71ebc8] \q[%(upchec),OnButtonChange,upchec,%(upchec)]\f[default]\n"
    --
	"\![*]\f[bold,1]\q[Reset,resetuser]\f[default]\n\n"
	--	
	"\![*]\q[Back to the menu,BACKTOMENU]\e"
}

Select.hemisph
{
	if hsphere == "Northern"; hsphere = "Eastern"
	else; hsphere = "Northern"
}

Select.resetuser
{
	MiscFlags ,= "Reset"
	--
	OnFirstBoot
}

Select.datetype
{
	case dateformatype
	{
		when "dd/mm/yyyy"; dateformatype = "yyyy/mm/dd"
		when "yyyy/mm/dd"; dateformatype = "mm/dd/yyyy"
		when "mm/dd/yyyy"; dateformatype = "dd/mm/yyyy"
	}
	--
	TalkConfig
}

Select.timetype
{
	case timeformatype
	{
		when "STANDARD"; timeformatype = "MILITARY"
		when "MILITARY"; timeformatype = "STANDARD"
	}
	--
	TalkConfig
}

SetOnOffConf
{
	if upchec == ""; upchec = "OFF"
	--
	if hsphere == ""; hsphere = "Northern"
	--
	if moonmenu == ""; moonmenu = "OFF"
	--
	if weathermenu == ""; weathermenu = "OFF"
	--
}